open Conex_utils

(* this is the barebones verify with minimal dependencies
   (goal: cmdliner, opam-file-format, Unix, external openssl)
 *)

(* to be called by opam (see http://opam.ocaml.org/doc/2.0/Manual.html#configfield-repository-validation-command, https://github.com/ocaml/opam/pull/2754/files#diff-5f9ccd1bb288197c5cf2b18366a73363R312):

%{quorum}% - a non-negative integer (--quorum)
%{anchors}% - list of digests, separated by "," (--trust-anchors -- to be used in full verification)
%{root}% - the repository root (--repository)

(we need --strict and --no-strict [initially default])

two modes of operation (%{incremental}% will just be "true" or "false"):

-full
%{dir}% is only defined for a full update, and is the dir to verify (--dir)

-incremental
%{patch}% - path to a patch (to be applied with -p1, generated by diff -ruaN dir1 dir2) (--patch)

exit code success = 0, failure otherwise

example:

repository-validation-command: [
   "conex" "--root" "%{root}%" "--trust-anchors" "%{anchors}%" "--patch" "%{patch}%"
]

> cat conex
#!/bin/bash -ue
echo "$*"
true

 *)

module IO = Conex_io

module VERIFY (L : LOGS) (V : Conex_verify.S) = struct

  module C = Conex.Make(L)(V)

  let verify_diff io patch valid quorum ignore_missing root_file opam =
    Conex_unix_persistency.read_file patch >>= fun x ->
    let newio, diffs = Conex_diff_provider.apply_diff io x in
    (* C.verify_snapshot newio repo >>= fun () -> *)
    C.verify_root ~valid ?quorum newio root_file >>= fun repo ->
    C.verify ~ignore_missing newio repo opam >>= fun () ->
    C.verify_diffs root_file io newio diffs opam >>= fun () ->
    Printf.printf "diff verification successfull\n" ;
    Ok ()

  let verify_full io valid quorum ignore_missing root_file opam =
    C.verify_root ~valid ?quorum io root_file >>= fun repo ->
    C.verify ~ignore_missing io repo opam >>= fun () ->
    Printf.printf "full verification successfull\n" ;
    Ok ()

  let verify_it repodir quorum anchors incremental dir patch nostrict root_file opam =
    let valid = Conex_opts.valid anchors in
    match repodir, incremental, patch, dir with
    | Some repodir, true, Some p, None ->
      Conex_unix_provider.fs_ro_provider repodir >>= fun io ->
      L.debug (fun m -> m "repository %a" Conex_io.pp io) ;
      verify_diff io p valid quorum nostrict root_file opam
    | _, false, None, Some "" ->
      L.debug (fun m -> m "called with no incremental, and dir = empty -> no update") ;
      Ok ()
    | _, false, None, Some d ->
      Conex_unix_provider.fs_ro_provider d >>= fun io ->
      L.debug (fun m -> m "repository %a" Conex_io.pp io) ;
      verify_full io valid quorum nostrict root_file opam
    | None, _, _, _ -> Error "--repo is required"
    | _ -> Error "invalid combination of incremental, patch and dir"
end

let doc = "Verify a signed community repository"
and man = [
  `S "DESCRIPTION" ;
  `P "$(tname) verifies a cryptographically signed community repository" ;
  `P "Two verification modes are supported: $(b,initial) and $(b,incremental)." ;
  `P "During $(b,initial) verification, the trust is rooted in $(b,--anchors), and the repository $(b,--dir) is verified." ;
  `P "In $(b,incremental) mode, an existing trusted repository is used as trust root, and a provided $(b,--patch) is verified: signed and monotonicity is preserved.";
]
