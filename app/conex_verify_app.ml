open Conex_utils

(* this is the barebones verify with minimal dependencies
   (goal: cmdliner, opam-file-format, Unix, external openssl)
 *)

(* to be called by opam (see http://opam.ocaml.org/doc/2.0/Manual.html#configfield-repository-validation-command, https://github.com/ocaml/opam/pull/2754/files#diff-5f9ccd1bb288197c5cf2b18366a73363R312):

%{quorum}% - a non-negative integer (--quorum)
%{anchors}% - list of digests, separated by "," (--trust-anchors -- to be used in full verification)
%{root}% - the repository root (--repository)

(we need --strict and --no-strict [initially default])

two modes of operation (%{incremental}% will just be "true" or "false"):

-full
%{dir}% is only defined for a full update, and is the dir to verify (--dir)

-incremental
%{patch}% - path to a patch (to be applied with -p1, generated by diff -ruaN dir1 dir2) (--patch)

exit code success = 0, failure otherwise

example:

repository-validation-command: [
   "conex" "--root" "%{root}%" "--trust-anchors" "%{anchors}%" "--patch" "%{patch}%"
]

> cat conex
#!/bin/bash -ue
echo "$*"
true

 *)

module IO = Conex_io

let ( let* ) = Result.bind

module VERIFY (L : LOGS) (V : Conex_verify.S) = struct

  module C = Conex.Make(L)(V)

  let verify_root_targets io valid quorum ignore_missing root_file opam ~timestamp_expiry ~now =
    let* repo = C.verify_root ~valid ?quorum io root_file in
    let* () = C.verify_timestamp io repo ~timestamp_expiry ~now in
    (*    C.verify_snapshot io repo >>= fun () -> *)
    C.verify ~ignore_missing io repo opam

  let verify_diff io patch valid quorum ignore_missing root_file opam ~timestamp_expiry ~now =
    let* x = Conex_unix_persistency.read_file patch in
    let newio, diffs = Conex_diff_provider.apply_diff io x in
    let* () =
      verify_root_targets newio valid quorum ignore_missing root_file opam ~timestamp_expiry ~now
    in
    let* () = C.verify_diffs root_file io newio diffs opam in
    Printf.printf "diff verification successfull\n" ;
    Ok ()

  let verify_full io valid quorum ignore_missing root_file opam ~timestamp_expiry ~now =
    let* () =
      verify_root_targets io valid quorum ignore_missing root_file opam ~timestamp_expiry ~now
    in
    Printf.printf "full verification successfull\n" ;
    Ok ()

  let verify_it repodir quorum anchors incremental dir patch nostrict root_file opam ~timestamp_expiry ~now =
    let valid = Conex_opts.valid anchors in
    match repodir, incremental, patch, dir with
    | Some repodir, true, Some p, None ->
      let* io = Conex_unix_provider.fs_ro_provider repodir in
      L.debug (fun m -> m "repository %a" Conex_io.pp io) ;
      verify_diff io p valid quorum nostrict root_file opam ~timestamp_expiry ~now
    | _, false, None, Some "" ->
      L.debug (fun m -> m "called with no incremental, and dir = empty -> no update") ;
      Ok ()
    | _, false, None, Some d ->
      let* io = Conex_unix_provider.fs_ro_provider d in
      L.debug (fun m -> m "repository %a" Conex_io.pp io) ;
      verify_full io valid quorum nostrict root_file opam ~timestamp_expiry ~now
    | None, _, _, _ -> Error "--repo is required"
    | _ -> Error "invalid combination of incremental, patch and dir"
end

let doc = "Verify a signed community repository"
and man = [
  `S "DESCRIPTION" ;
  `P "$(tname) verifies a cryptographically signed community repository" ;
  `P "Two verification modes are supported: $(b,initial) and $(b,incremental)." ;
  `P "During $(b,initial) verification, the trust is rooted in $(b,--anchors), and the repository $(b,--dir) is verified." ;
  `P "In $(b,incremental) mode, an existing trusted repository is used as trust root, and a provided $(b,--patch) is verified: signed and monotonicity is preserved.";
]
